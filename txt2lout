#!/usr/bin/perl -w

# $Id: txt2lout,v 1.12 1999/08/08 15:15:55 root Exp root $

# Copyright (c) 1999 Mark Summerfield. All Rights Reserved.
# May be used/distributed under the GPL.

use strict ; 

use Getopt::Long ;
use Lout ;


use vars qw( $VERSION ) ;
$VERSION         = '1.01' ;

my $Outfile      = 'STDOUT' ;
my $Wrapper      = 1 ;
my $SmartQuotes  = 1 ;
my $Superscripts = 1 ;
my $ExtraSpace   = 0 ;

# main

&process_args ;
if( $Outfile ne 'STDOUT' ) {
    open OUT, ">$Outfile" or die "Failed to open $Outfile: $!\n" ;
    select( OUT ) ;
}

&head if $Wrapper ;

while( <> ) {
    if( /^\s*$/o ) {
        # Paragraphs separated by blank lines.
        print "\@PP\n$ExtraSpace" ;
    }
    else {
        print Lout::txt2lout( $_ ) ;
    }
}

&tail if $Wrapper ;


sub process_args {

    Getopt::Long::config 'no_ignore_case' ;
    GetOptions(
        'h|help'            => \&help,
        'e|extraspace=i'    => \$ExtraSpace,
        'o|outfile=s'       => \$Outfile,
        'q|smartquotes=i'   => \$SmartQuotes,
        's|superscripts=i'  => \$Superscripts,
        'w|wrapper=i'       => \$Wrapper,
        ) or die "\n" ;

    Lout::set_option( -smart_quotes => $SmartQuotes ) ;
    Lout::set_option( -superscripts => $Superscripts ) ;

    $ExtraSpace = $ExtraSpace ? "\n" : '' ;
}


sub help {
    print <<__EOT__ ;
txt2lout v $VERSION. Copyright (c) Mark Summerfield 1999. All rights reserved.
This is free software you can use/modify it under the same terms as perl.

usage: txt2lout [options] <files>

options:
-h    --help          Print this help screen and exit
-e    --extraspace    Print blank lines between paras [$ExtraSpace]
-o F  --outfile F     Write to file F [$Outfile]
-q    --smartquotes   Smartquotes [$SmartQuotes]
-s    --superscripts  Superscripts for 1st, 2nd etc [$Superscripts]
-w    --wrapper       Wrap text in lout \@Begin..\@End [$Wrapper]

The -e option is useful for troff and tex spacing.
e.g. for use in an editor like vim:
    .,\$!txt2lout -w 0 -e 1
e.g. from the command line:
    txt2lout file.txt > file.lt ; lout -s file.lt > file.ps
__EOT__
    exit ;
}


sub head {
 
    my( $d, $m, $y ) = (localtime( time ))[3..5] ;
    $y += 1900 ; $m++ ; 
    $m = "0$m" if $m < 10 ; 
    $d = "0$d" if $d < 10 ;

    print <<__EOT__ ;
\@SysInclude { tbl }
\@SysInclude { doc }
# Created by txt2lout on $y/$m/$d.
\@Doc \@Text \@Begin
__EOT__
}


sub tail {
    print <<__EOT__ ;
\@End \@Text
__EOT__
}


__END__


=pod SCRIPT CATEGORIES

Lout

=pod DESCRIPTION

Converts plain text to Lout

=pod PREREQUISITES

Pragmas:

C<strict>

Modules:

C<Getopt::Long>
C<Lout>

=pod OSNAMES

Linux

=pod LICENSE

GPL.

=cut
